IF
	Global("OublekBounty1","GLOBAL",1)
	Global("BrageRiddle","GLOBAL",5)
	Global("C#Q13_BragesSwordQuest","GLOBAL",0)
	Exists("Brage")
	InMyArea("Brage")
	!Dead("Brage")
THEN
	RESPONSE #100
		FadeToColor([20.0],0)
		Wait(1)
		SetGlobal("C#Q13_BragesSwordQuest","GLOBAL",1)
		ActionOverride("Brage",DestroySelf())
		FadeFromColor([20.0],0)
		Continue()
END

IF
	Global("OublekBounty1","GLOBAL",1)
	!Dead("Brage")
	Global("C#Q13_BragesSwordQuest","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Q13_BragesSwordQuest","GLOBAL",1)
		Continue()
END


/* Laryssa spawn */
/* [Borda is still alive and has to appear a second time.] Laryssa spawns outside temple in Nashkel after Nalin looked at the sword */
IF
	Global("C#q13_LarissaSpawn","MYAREA",0)
	OR(2)
		Global("C#q13Brage_FakeMerchant","GLOBAL",0)
		Global("C#q13Brage_FakeMerchant","GLOBAL",1)
	!Dead("BORDA") 
	!Dead("Brage")
	GlobalGT("C#q13Brage_NalinVouch","GLOBAL",1)
	OR(2)
		GlobalLT("C#Q13_BragesSwordQuest","GLOBAL",4)
		Global("C#Q13_BragesSwordQuest","GLOBAL",6)
	!Dead("laryssa")
THEN
	RESPONSE #100
		SetGlobal("C#q13_LarissaSpawn","MYAREA",1)
		CreateCreature("c#q13lar",[2450.1198],13)
		IncrementGlobal("C#q13Brage_FakeMerchant","GLOBAL",2)
		Continue()
END

/* in case innocence was proven with third letter out of Tazok's tent (PC didn't need Larissa to find the merchant */
IF
	Global("C#q13_LarissaSpawn","MYAREA",0)
	Dead("BORDA") 
	!Dead("Brage")
	GlobalGT("C#q13Brage_NalinVouch","GLOBAL",1)
	OR(2)
		GlobalLT("C#Q13_BragesSwordQuest","GLOBAL",4)
		Global("C#Q13_BragesSwordQuest","GLOBAL",6)
	!Dead("laryssa")
THEN
	RESPONSE #100
		SetGlobal("C#q13_LarissaSpawn","MYAREA",1)
		CreateCreature("c#q13lar",[2450.1198],13)
		Continue()
END

/* remove journal entry from Nalin's hint if PC got the quest */

IF
	InMyArea(Player1)
	GlobalGT("C#q13Brage_NalinVouch","GLOBAL",0)
	GlobalGT("C#BE_NalinHint","GLOBAL",0)
	GlobalLT("C#BE_NalinHint","GLOBAL",3)
THEN
  RESPONSE #100
		SetGlobal("C#BE_NalinHint","GLOBAL",3)
		EraseJournalEntry(@1022)
		Continue()
END

/* Oublek will initiate dialogue if Brage was brought to him - there is no "See(Player1)" check because this assumes that Brage was just brought to Oublek and the PC is still near Oublek!*/
IF
	Global("OublekBounty1","GLOBAL",1)
	Global("BrageRiddle","GLOBAL",5)
	Global("C#Q13_BragesSwordQuest","GLOBAL",1)
	InMyArea("Oublek")
	!Dead("Oublek")
THEN
	RESPONSE #100
		ActionOverride("Oublek",StartDialogNoSet(Player1))
		Continue()
END

/* remove other journal entries after letter was handed to Oublek */
IF
	InMyArea(Player1)
	GlobalGT("C#Q13_BragesSwordQuest","GLOBAL",2)
	Global("C#BE_JournalRemoval4800","MYAREA",0)
THEN
  RESPONSE #100
		SetGlobal("C#BE_JournalRemoval4800","MYAREA",1)
		EraseJournalEntry(@1023)
		EraseJournalEntry(@1024)
		Continue()
END
