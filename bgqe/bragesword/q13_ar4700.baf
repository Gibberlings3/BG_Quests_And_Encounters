/* check, whether Borda was met the first time and is still alive */

/* new for v25.6: IncrementGlobal("C#q13Brage_FakeMerchant","GLOBAL",1) is set in Borda's greeting's dialogue no matter what. Correct the variable if he's killed and letter is picked up afterwards: - hopefully, this will prevent the quest from not kicking in because the player left the area too quickly */
/* correct variable if letter is picked up */
IF
	OR(4)
		Global("C#q13Brage_FakeMerchant","GLOBAL",1)
		Global("C#q13Brage_FakeMerchant","GLOBAL",3)
		Global("C#q13Brage_FakeMerchant","GLOBAL",5)
		Global("C#q13Brage_FakeMerchant","GLOBAL",7)
	Dead("BORDA")
	PartyHasItem("c#q13le2") //letter on Borda
THEN
	RESPONSE #100
		IncrementGlobal("C#q13Brage_FakeMerchant","GLOBAL",-1)
		Continue()
END

/* ald script blocks: I'll leave them in in case another mod adds to Borda after bgqe where the ADD_TRANS_ACTION was not patched to */ 
/* met Borda before quest */
IF
	Global("C#q13Brage_FakeMerchant","GLOBAL",0)
	!Exists("BORDA")
	!Dead("BORDA")
	!PartyHasItem("c#q13le2") //letter on Borda
	!Global("C#Q13_BragesSwordQuest","GLOBAL",55) //quest is not dead
THEN
	RESPONSE #100
		SetGlobal("C#q13Brage_FakeMerchant","GLOBAL",1)
		Continue()
END

/* PC met Borda for active quest but didn't confront him - variable has to be incremented so MYSMER points PC into the right direction */

IF
	OR(3)
	Global("C#q13Brage_FakeMerchant","GLOBAL",2)
	Global("C#q13Brage_FakeMerchant","GLOBAL",4)
	Global("C#q13Brage_FakeMerchant","GLOBAL",6)
	!Exists("BORDA")
	!Dead("BORDA")
	!PartyHasItem("c#q13le2") //letter on Borda
	!Global("C#Q13_BragesSwordQuest","GLOBAL",55) //quest is not dead
THEN
	RESPONSE #100
		IncrementGlobal("C#q13Brage_FakeMerchant","GLOBAL",1)
		Continue()
END

/* journal entry if PC found the quest items (letter and Brage's Sword) */

IF
	InMyArea(Player1)
	!Global("OublekBounty1","GLOBAL",1)
	Dead("Brage")
	PartyHasItem("c#q13swd")
	PartyHasItem("c#q13le2")
	GlobalLT("C#BE_JournalEntries","MYAREA",0)
THEN
  RESPONSE #100
		SetGlobal("C#BE_JournalEntries","MYAREA",1)
		AddJournalEntry(@1023,QUEST)
		Continue()
END

IF
	InMyArea(Player1)
	Global("OublekBounty1","GLOBAL",1)
	Dead("Brage")
	PartyHasItem("c#q13swd")
	PartyHasItem("c#q13le2")
	GlobalLT("C#BE_JournalEntries","MYAREA",0)
THEN
  RESPONSE #100
		SetGlobal("C#BE_JournalEntries","MYAREA",1)
		AddJournalEntry(@1024,QUEST)
		Continue()
END
